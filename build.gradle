def theGroup = 'org.mirah'
def theName = 'mirahc'
def theVersion = System.env.MIRAH_VERSION_MAJOR + '.' + System.env.MIRAH_VERSION_MINOR


buildscript {
	repositories {
		maven { url project.properties['repo.url'] ?: System.env.REPO_URL }
	}

	dependencies {
		classpath group: 'cz.kubacki.gradle.plugins', name: 'gradle-nbm-plugin', version: '1.11.0'
	}
}



subprojects {
	repositories {
		maven { url project.properties['repo.url'] ?: System.env.REPO_URL }
	}
}



project(':mirahc3') {
	apply plugin: 'java'

	dependencies {
		compile files('../dist/mirahc3.jar')
	}

	apply plugin: 'maven-publish'

	publishing {
		repositories {
			maven {
				url project.properties['publish.url'] ?: System.env.PUBLISH_URL
			}
		}

		publications {
			mirahc3(MavenPublication) {
				groupId theGroup
				artifactId theName
				version theVersion

				artifact '../dist/mirahc3.jar'
				artifact source: '../dist/mirahc-stub.jar', classifier: 'source'
			}
		}
	}
}



project(':nbm') {
	apply plugin: 'cz.kubacki.nbm'

	description = 'The Mirah Programming Language'
	nbm {
		moduleName = "${theGroup}.${theName}.nbm"
		specificationVersion = implementationVersion = theVersion
		friendPackages {
			add 'mirah.**'
			add 'org.mirah.**'
		}
	}

	dependencies {
		compile group: theGroup, name: theName, version: theVersion
	}

	apply plugin: 'maven-publish'

	publishing {
		repositories {
			maven {
				url project.properties['publish.url'] ?: System.env.PUBLISH_URL
			}
		}
		publications {
			nbm(MavenPublication) {
				groupId theGroup
				artifactId "$theName-nbm"
				version theVersion

				from components.java

				artifact tasks.nbm.outputFile

				// workaround
				// https://discuss.gradle.org/t/maven-publish-plugin-generated-pom-making-dependency-scope-runtime/7494/6
				pom.withXml {
					asNode().dependencies.'*'.findAll() {
						it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
							dep.name == it.artifactId.text()
						}
					}.each() {
						it.scope*.value = 'compile'
					}
				}

			}
		}
	}

	tasks.publish.dependsOn tasks.nbm

}
